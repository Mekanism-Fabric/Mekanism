plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT' apply false
	id 'maven-publish'
	id "java-library"
	id 'idea'
}

class Globals {
	static def baseVersion = "1.0.0"
	static def mcVersion = "1.18.2"
	static def loaderVersion = "0.14.6"
	static def maven_group = "mekanism"
	static def fabricVersion = "0.51.1+1.18.2"
	static def modMenuVersion = "3.2.2"
	static def wthitVersion = "4.10.3"
	static def techRebornEnergyVersion = "2.1.0"
	static def reiVersion = "8.1.457"
}

version = Globals.baseVersion
logger.lifecycle("Building Mekanism: " + version)

static def moduleDependencies(project, List<String> depNames) {
	def deps = depNames.iterator().collect { project.dependencies.project(path: ":$it", configuration: 'devElements') }
	project.dependencies {
		deps.each {
			api it
		}
	}
	project.publishing {
		publications {
			mavenJava(MavenPublication) {
				pom.withXml {
					def depsNode = asNode().appendNode("dependencies")
					deps.each {
						def depNode = depsNode.appendNode("dependency")
						depNode.appendNode("groupId", it.group)
						depNode.appendNode("artifactId", it.name)
						depNode.appendNode("version", it.version)
						depNode.appendNode("scope", "compile")
					}
				}
			}
		}
	}
}

allprojects {
	apply plugin: "java-library"
	apply plugin: "fabric-loom"
	apply plugin: "maven-publish"
	apply plugin: "idea"

	tasks.withType(JavaCompile).configureEach {
		// ensure that the encoding is set to UTF-8, no matter what the system default is
		// this fixes some edge cases with special characters not displaying correctly
		// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
		// If Javadoc is generated, this must be specified in that task too.
		it.options.encoding = "UTF-8"

		// Minecraft 1.17 (21w19a) upwards uses Java 16.
		it.options.release = 17
	}

	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
	group = Globals.maven_group
	configurations { devElements { extendsFrom implementation, namedElements } }

	sourceSets {
		testmod {
			compileClasspath += main.compileClasspath
			runtimeClasspath += main.runtimeClasspath
		}
	}

	loom {
		runs {
			testmodClient {
				client()
				ideConfigGenerated project.rootProject == project
				name = "Testmod Client"
				source sourceSets.testmod
			}
			testmodServer {
				server()
				ideConfigGenerated project.rootProject == project
				name = "Testmod Server"
				source sourceSets.testmod
			}
		}
	}

	dependencies {
		minecraft "com.mojang:minecraft:$Globals.mcVersion"
		mappings loom.layered() {
			officialMojangMappings()
			parchment("org.parchmentmc.data:parchment-1.18.2:2022.05.02@zip")
		}
		modApi "net.fabricmc:fabric-loader:${Globals.loaderVersion}"
		modImplementation "net.fabricmc.fabric-api:fabric-api:${Globals.fabricVersion}"

		compileOnly 'org.projectlombok:lombok:1.18.20'
		annotationProcessor 'org.projectlombok:lombok:1.18.20'

		modRuntimeOnly("com.terraformersmc:modmenu:${Globals.modMenuVersion}") {
			transitive(false)
		}

		modCompileOnly "mcp.mobius.waila:wthit-api:fabric-${Globals.wthitVersion}"
		modRuntimeOnly "mcp.mobius.waila:wthit:fabric-${Globals.wthitVersion}"

		modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:${Globals.reiVersion}"
		modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${Globals.reiVersion}"

		implementation 'com.electronwill.night-config:toml:3.6.4'
		implementation 'com.electronwill.night-config:core:3.6.4'


		//https://github.com/TechReborn/Energy/issues/18
		include modApi("teamreborn:energy:2.1.0") {
			exclude(group: "net.fabricmc.fabric-api")
		}

		modImplementation "curse.maven:ForgeConfigAPIPort-547434:3600918"

		modImplementation(include("io.github.fabricators_of_create:Porting-Lib:1.1.0-beta+1.18.2-dev.ffa2ca8"))

		subprojects.each {
			implementation project(path: ":${it.name}", configuration: "devElements")
			include project("${it.name}:")
		}
	}

	configurations { dev }
	loom { shareRemapCaches = true }

	repositories {
		mavenLocal()
		maven { url "https://maven.shedaniel.me" }
		maven { url "https://maven.terraformersmc.com/releases" }
		maven { url "https://bai.jfrog.io/artifactory/maven" }
		maven { url "https://maven.bai.lol" }
		maven { url "https://cursemaven.com" }
		maven { url "https://maven.parchmentmc.org"}
		maven { url = "https://mvn.devos.one/releases/" }
		maven { url = "https://mvn.devos.one/snapshots/" }
		maven { url = "https://jitpack.io/" }
		mavenCentral()
	}

	jar {
		from("LICENSE") {
			rename { "${it}_${project.archivesBaseName}"}
		}
		archiveClassifier = "dev"
	}

	afterEvaluate {
		processResources {
			inputs.property "version", project.version

			filesMatching("fabric.mod.json") {
				expand "version": project.version
			}
		}
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		archiveClassifier = "sources"
		from sourceSets.main.allSource
	}

	tasks.withType(AbstractArchiveTask) {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
	}

	java {
		withSourcesJar()
	}
}

//dependencies {
//	afterEvaluate {
//		subprojects.each {
//			implementation project(path: ":${it.name}", configuration: "devElements")
//			include project("${it.name}:")
//
//			testmodImplementation project("${it.name}:").sourceSets.testmod.output
//		}
//	}
//}